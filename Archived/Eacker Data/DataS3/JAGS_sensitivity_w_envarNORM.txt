######################################################################################
# JAGS code for Bitterroot Elk Population IPM sensitivity model 
#(with environmental variance estimation)
######################################################################################
#begin data statement for right censoring indicator
data{
#Data for exponential adult female survival model-annual
	for(t in 1:(nYears-1)){	
		for(i in 1:(n.obsAF[t])){	
			oneAF[t,i] <- 1  
   }
}  
#Data for weibull calf survival model-summer
	for(t in 1:(nYears-1)){	
		for(i in 1:(n.obsS[t])){	
			oneS[t,i] <- 1  
   }
}  
#Data for exponential calf survival model-winter

	for(t in 1:(nYears-1)){	
		for(i in 1:(n.obsW[t])){	
			oneW[t,i] <- 1  
   }
}  
}
#start model
model{	
	
######################################################################################
# Initial population size (priors) at time 1 as truncated normal distribution
		Ny[1]~dnorm(NinitY, 0.00001)T(0,) 	  #number of yearlings at time 1
		Naf[1]~dnorm(NinitAF, 0.00001)T(0,)  #number of adult females at time 1
			
######################################################################################
# Process model (aka biology)
	
	for(t in 2:nYears){
#Derive number of calves, adults from yearlings, and adults
		meanNy[t] <- Sc[t-1]*Preg[t-1]*Naf[t-1]
		meanNaf[t] <- (Saf[t-1]*Naf[t-1]) + (Sy*(Ny[t-1]*P))
		
		Ny[t] ~ dpois(meanNy[t])
		Naf[t] ~ dpois(meanNaf[t])		
}		
	for(t in 1:nYears){
#Derive total population size
		Ntot[t] <- Ny[t] + Naf[t]
#Derive proportion of population that is yearling 
		propY[t]<-(Ny[t])/(Ntot[t])
#Derive proportion of population that is adult female 
		propAF[t]<-(Naf[t])/(Ntot[t])
}

#Derive annual growth rates (with numerical offset)
	for(t in 1:(nYears-1)){
		pop.growth[t]<-((Ntot[t+1] + 0.000001)/(Ntot[t] + 0.000001))
}
#Derive arithmetic mean growth rate
		meanGROWTH<- sum(pop.growth)/(nYears-1)
#Derive geometric mean growth rate
		medianGROWTH<-(prod(pop.growth))^(1/(nYears-1))

######################################################################################
#Observation models (aka. Likelihoods for telemetry and pregnancy data)
#Adult female survival
	for(t in 1:(nYears-1)){
			lambdaAF[t] <- -log(Saf[t])/365
		for(i in 1:(n.obsAF[t])){	
			oneAF[t,i]~dinterval(yAF[t,i], y.censAF[t,i])	
			yAF[t,i]~dexp(lambdaAF[t])T(y.entAF[t,i], )		
}					
}
######################################################################################
#Pregnancy data from collared adult females 		
	for(t in 1:(nYears-1)){	
		preg[t] ~ dbin(Preg[t], afCollars[t])
}							
######################################################################################
#summer calf survival
	for(t in 1:(nYears-1)){
			lambdaS[t] <- -log(ScS[t])/pow(185, shapeS)
		for(i in 1:(n.obsS[t])){
			oneS[t,i]~dinterval(yS[t,i], y.censS[t,i])	
			yS[t,i]~dweib(shapeS, lambdaS[t])T(y.entS[t,i], )		
	}			
}	
#winter calf survival
	for(t in 1:(nYears-1)){
		lambdaW[t] <- -log(ScW[t])/180
		for(i in 1:(n.obsW[t])){	
			oneW[t,i]~dinterval(yW[t,i], y.censW[t,i])	
			yW[t,i]~dexp(lambdaW[t])T(y.entW[t,i], )
			}			
	}
#derive annual calf survival
	for(t in 1:(nYears-1)){
		Sc[t] <- ScS[t] * ScW[t]
}
######################################################################################
##Survival and pregnancy probabilities and environmental variances, and associated priors
	
#priors for shape parameter
shapeS ~ dunif(0, 2)
#informative prior for yearling survival based on Raithel et al. (2007)
		Sy~dbeta(21.92, 2.90)		
#priors for adult female survival probability and temporal variance on the logit scale
		l.Saf~dnorm(0,0.5)T(-10, 10)
#prior on standard deviation
		sig.Saf~dunif(0,10) 
		tau.Saf<-pow(sig.Saf, -2)
		var.Saf<-1/tau.Saf
		var.Saf.real<-(var.Saf * meanSaf^2 * (1-meanSaf)^2)
#logit of adult female probability with random temporal effect		
for(t in 1:(nYears-1)){	
		epsilon.Saf[t]~dnorm(0,tau.Saf)T(-15,15)
		logit(Saf[t]) <- l.Saf + epsilon.Saf[t]
		}

#priors for pregnancy probability and temporal variance on the logit scale
		l.Preg~dnorm(0,0.5)T(-10, 10)
#prior on standard deviation
		sig.Preg~dunif(0,10) 
		tau.Preg<-pow(sig.Preg, -2)
		var.Preg<-1/tau.Preg
#temporal variance of pregnancy on probability scale
		var.Preg.real<-(var.Preg * meanPreg^2 * (1-meanPreg)^2)
#logit of pregnancy probability with random temporal effect
	for(t in 1:(nYears-1)){	
		epsilon.Preg[t]~dnorm(0,tau.Preg)T(-15,15)
		logit(Preg[t]) <- l.Preg + epsilon.Preg[t]   
}
		
#priors for summer calf survival probability and temporal variance on the logit scale			
		l.ScS~dnorm(0,0.5)T(-10, 10)
#prior on standard deviation
		sig.ScS~dunif(0,10) 
		tau.ScS<-pow(sig.ScS, -2)
		var.ScS<-1/tau.ScS
#temporal variance of summer calf survival on probability scale
		var.ScS.real<-(var.ScS * meanScS^2 * (1-meanScS)^2)
#logit of summer calf survival probability with random temporal effect		
	for(t in 1:(nYears-1)){	
		epsilon.ScS[t]~dnorm(0,tau.ScS)T(-15,15)
		logit(ScS[t]) <- l.ScS + epsilon.ScS[t]    	
}

#priors for winter calf survival probability and variance on the logit scale
		l.ScW~dnorm(0,0.5)T(-10, 10)
#prior on standard deviation
		sig.ScW~dunif(0,10) 
		tau.ScW<-pow(sig.ScW, -2)
		var.ScW<-1/tau.ScW
#temporal variance of winter calf survival on probability scale
		var.ScW.real<-(var.ScW * meanScW^2 * (1-meanScW)^2)
#logit of winter calf survival probability with random temporal effect			
	for(t in 1:(nYears-1)){	
		epsilon.ScW[t]~dnorm(0,tau.ScW)T(-15,15)
		logit(ScW[t]) <- l.ScW + epsilon.ScW[t]   
}

#mean vital rates transformed from logit to probability scale
        meanSaf<-exp(l.Saf)/(1+exp(l.Saf))
		meanPreg<-exp(l.Preg)/(1+exp(l.Preg))
		meanScS<-exp(l.ScS)/(1+exp(l.ScS))
		meanScW<-exp(l.ScW)/(1+exp(l.ScW))
#calculate mean annual calf survival
		meanSc <- meanScS * meanScW

# create LSA sampling distributions using estimates of mean vital rates and environmental variances
# note that beta distributions could also be constructed, but normal distributions are
# more effecient to sample from in JAGS

		mSaf ~ dnorm(meanSaf, 1/var.Saf.real)T(0,1)
		mPreg ~ dnorm(meanPreg, 1/var.Preg.real)T(0,1)
		mScS ~ dnorm(meanScS, 1/var.ScS.real)T(0,1)
		mScW ~ dnorm(meanScW, 1/var.ScW.real)T(0,1)	
		mSc <- mScS * mScW
		
#######################################################################################
#sensitivity analysis
	
#terms of quadratic (i.e., characteristic) equation	
	Q1<- 1
	Q2<- (-mSaf)
	Q3<- (-(Sy*P*(mSc*mPreg)))
	
#eig.value1 is dominant eigenvalue or asymptotic growth rate
    eig.value1<- (-Q2 + (sqrt((Q2^2) - (4*Q1*Q3))))/(2*Q1)
	eig.value2<- (-Q2 - (sqrt((Q2^2) - (4*Q1*Q3))))/(2*Q1)
	
	Id1<-eig.value1*Identity
	Id2<-eig.value2*Identity

	mat[1,1]<-0
	mat[1,2]<-(mSc*mPreg)
	mat[2,1]<-(Sy*P)
	mat[2,2]<-mSaf
	
	new.mat1<-mat-Id1
	new.mat2<-mat-Id2
	
	eig.vec1<-1
	eig.vec2<-(new.mat1[1,1])/(-new.mat1[1,2])
	eig.vect1<-eig.vec1/sqrt(1 + (eig.vec2^2))
	eig.vect2<-eig.vec2/sqrt(1 + (eig.vec2^2))
	
	eig.vec3<-1
	eig.vec4<-(new.mat2[1,1])/(-new.mat2[1,2])
	eig.vect3<-eig.vec3/sqrt(1 + (eig.vec4^2))
	eig.vect4<-eig.vec4/sqrt(1 + (eig.vec4^2))
	
	sensMAT[1,1]<-eig.vect1
	sensMAT[1,2]<-eig.vect3
	sensMAT[2,1]<-eig.vect2
	sensMAT[2,2]<-eig.vect4
	
	inMAT[1,1]<-sensMAT[2,2]
	inMAT[1,2]<-(-sensMAT[1,2])
	inMAT[2,1]<-(-sensMAT[2,1])
	inMAT[2,2]<-sensMAT[1,1]
	
	v <-(1/((sensMAT[1,1]*sensMAT[2,2])-(sensMAT[1,2]*sensMAT[2,1])))*inMAT
	v1<-v[1,]
	w1<-sensMAT[1:2,1]

#Stable stage distribution
	SSD[1]<-w1[1]/(w1[1]+w1[2])
	SSD[2]<-w1[2]/(w1[1]+w1[2])
#reproductive values
	RV[1]<-v1[1]/(v1[1])
	RV[2]<-v1[2]/(v1[1])
#sensitivities
	sens[1,1]<-v1[1]*w1[1]
	sens[1,2]<-v1[1]*w1[2]
	sens[2,1]<-v1[2]*w1[1]
	sens[2,2]<-v1[2]*w1[2]
#elasticities	
	elas<-(mat/eig.value1) * sens
#component elasticities
	cmp.ScS<-(sens[1,2]*mScS)*(mScS/eig.value1)
	cmp.ScW<-(sens[1,2]*mScW)*(mScW/eig.value1)
	cmp.Sc<-(sens[1,2]*mSc)*(mSc/eig.value1)
	cmp.Saf<-(sens[2,2]*mSaf)*(mSaf/eig.value1)
	cmp.Preg<-(sens[1,2]*mPreg)*(mPreg/eig.value1)
	cmp.Sy<-(sens[2,1]*Sy)*(Sy/eig.value1)
	cmp.P<- (sens[2,1]*P)*(P/eig.value1)		
}
#End model
###################################################################################### 
